tmp_scoring = LOAD '/tmp/FIT5148/Scoring.csv' using PigStorage(',');
scoring_raw = FILTER tmp_scoring BY $0 != 'playerID' AND $16 >= 0;
scoring = FOREACH scoring_raw GENERATE $0 as playerID, $16 as GWG;

group_scoring = GROUP scoring ALL;
max_GWG = FOREACH group_scoring GENERATE MAX(scoring.GWG) as max_gwg;
join_max_scoring = JOIN max_GWG BY max_gwg, scoring BY GWG;
max_scoring_playerID = FOREACH join_max_scoring GENERATE $1 as playerID;
distinct_playerID = DISTINCT max_scoring_playerID;
--max_scoring_playerID
--0.playerID

tmp_awards_players = LOAD '/tmp/FIT5148/AwardsPlayers.csv' using PigStorage(',');
awards_players_raw = FILTER tmp_awards_players BY $2 > 0;
awards_players = FOREACH awards_players_raw GENERATE $0 as playerID, $1 as award;

join_distinct_playerID = JOIN distinct_playerID BY playerID, awards_players BY playerID;
--0.playerID, 1.playerID, 2.award

playerID_award = FOREACH join_distinct_playerID GENERATE $0 as playerID, $2 as award;

group_by_player = GROUP playerID_award BY (playerID);
awards_count = FOREACH group_by_player GENERATE group as playerID, 
	COUNT(playerID_award.award) as awards_number;
--awards_count
--0.playerID, 1.awards_number

group_awards_count = GROUP awards_count ALL;
max_awards_count = FOREACH group_awards_count GENERATE 
	MAX(awards_count.awards_number) as max_awards_number;
--max_awards_count
--max_awards_number

join_max_awards_count = JOIN max_awards_count BY max_awards_number, awards_count BY awards_number;
max_awards_player = FOREACH join_max_awards_count GENERATE $1 as playerID, $0 as awards_number;

tmp_master = LOAD '/tmp/FIT5148/Master.csv' using PigStorage(',');
master_raw = FILTER tmp_master BY $0 != 'playerID';
master = FOREACH master_raw GENERATE $0 as playerID, $3 as fname, $4 as lname;

join_master = JOIN max_awards_player BY playerID, master BY playerID;
--join_master
--0.playerID, 1.awards_number, 2.playerID, 3.fname, 4.lname

answer = FOREACH join_master GENERATE $3 as fname, $4 as lname, $1 as awards_number;

DUMP answer;