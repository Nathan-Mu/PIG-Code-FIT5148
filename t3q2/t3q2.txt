tmp_scoring = LOAD '/tmp/FIT5148/Scoring.csv' using PigStorage(',');
scoring_raw = FILTER tmp_scoring BY $1 > 0;
scoring = FOREACH scoring_raw GENERATE $0 AS playerID, $1 AS year, $3 AS tmID, $7 AS goals, 
	$8 AS assists, $9 AS points;
--scoring
--0.playerID, 1.year, 2.tmID, 3.goals, 4.assists, 5.points

group_by_tmID = GROUP scoring BY tmID;
max_points = FOREACH group_by_tmID GENERATE group as tmID, 
	MAX(scoring.points) as max_points;
    
join_max_points = JOIN max_points BY (tmID, max_points), scoring BY (tmID, points);
--join_max_points
--0.tmID, 1.max_points, 2.playerID, 3.year, 4.tmID, 5.goals, 6.assists, 7.points

max_points_info = FOREACH join_max_points GENERATE $0 as tmID, $2 as playerID, $3 as year,
	$7 as points, $5 as goals, $6 as assists;
--max_points_player
--0.tmID, 1.playerID, 2.year, 3.points, 4.goals, 5.assists

tmp_teams = LOAD '/tmp/FIT5148/Teams.csv' using PigStorage(',');
teams_raw = FILTER tmp_teams BY $0 > 0;
teams = FOREACH teams_raw GENERATE $2 AS tmID, $18 AS name;
--teams
--0.teamID, 1.name

distinct_team = DISTINCT teams;

join_max_points_info = JOIN max_points_info BY tmID, distinct_team BY tmID;
team_name = FOREACH join_max_points_info GENERATE $7 as name, $1 as playerID, $2 as year, 
	$3 as points, $4 as goals, $5 as assists;
    
tmp_master = LOAD '/tmp/FIT5148/Master.csv' using PigStorage(',');
master_raw = FILTER tmp_master BY $0 != 'playerID';
master = FOREACH master_raw GENERATE $0 as playerID, $3 as fname, $4 as lname;
    
join_team_name = JOIN team_name BY playerID, master BY playerID;
answer = FOREACH join_team_name GENERATE $0 as name, $7 as fname, $8 as lname, $2 as year, 
	$3 as points, $4 as goals, $5 as assists;
    
DUMP answer;